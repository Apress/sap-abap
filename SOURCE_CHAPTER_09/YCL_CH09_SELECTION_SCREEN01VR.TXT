*&---------------------------------------------------------------------*
*& Report  YCL_CH09_SELECTION_SCREEN01VR
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT YCL_CH09_SELECTION_SCREEN01VR
   MESSAGE-ID YCL_CH07_MCLASS01.

****************************************************
* SLECTION-SCREEN - Create SE38 Look Alike Screen **
* Use Function Module 'ICON_CREATE' To Put Icon & **
* Text On SLECTION-SCREEN Push Buttons            **
****************************************************

*********************************************************
* generate 2 blank lines:                              **
* SELECTION-SCREEN SKIP 2.                             **
*                                                      **
* three elements on the same line:                     **
*       (1) a label 'Program' for field                **
*       (2) a field (of type text box)                 **
*           to input the program name                  **
*       (3) a 'Create' push button                     **
*                                                      **
* SELECTION-SCREEN BEGIN OF LINE.                      **
* SELECTION-SCREEN COMMENT 1(20) TEXT-001.             **
* PARAMETERS PROGRAM TYPE TRDIRT-NAME MATCHCODE OBJECT **
*   ...... VISIBLE LENGTH 30.                          **
* SELECTION-SCREEN PUSHBUTTON 53(16) BUTTONCR          **
* USER-COMMAND CREAT.                                  **
* SELECTION-SCREEN END OF LINE.                        **
*                                                      **
* a gap of one line i.e.                               **
* SELECTION-SCREEN SKIP 1.                             **
*                                                      **
* START block with title 'Subobjects' & shaded effect  **
* extending to half the screen width:                  **
*                                                      **
* SELECTION-SCREEN BEGIN OF BLOCK BL1 WITH FRAME       **
* TITLE TEXT-003 NO INTERVALS.                         **
*                                                      **
* five radio buttons. each radio button with           **
* label on the same line:                              **
*                                                      **
* SELECTION-SCREEN BEGIN OF LINE.                      **
* PARAMETERS: SRCE RADIOBUTTON GROUP GRP.              **
* SELECTION-SCREEN COMMENT 2(20) TEXT-004.             **
* SELECTION-SCREEN END OF LINE.                        **
* ........                                             **
* ........                                             **
* a gap of one line after 5 radio buttons i.e.         **
* SELECTION-SCREEN SKIP 1.                             **
*                                                      **
* two push buttons ('Display' & 'Change') on same line **
*                                                      **
* SELECTION-SCREEN BEGIN OF LINE.                      **
* SELECTION-SCREEN PUSHBUTTON 1(16) BUTTONDS...        **
* .....                                                **
* ......                                               **
* SELECTION-SCREEN END OF LINE.                        **
*                                                      **
* SELECTION-SCREEN END OF BLOCK BL1                    **
*                                                      **
*********************************************************

*********************************************************
INCLUDE <ICON>. " icon definitions. required when you refer
                " to icon names, like we are doing in this
                " program

*********************************************************
SELECTION-SCREEN SKIP 2. " 2 blank lines at top

SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT 1(15) TEXT-001. " label 'Program'

  PARAMETERS PROGRAM TYPE TRDIRT-NAME MATCHCODE OBJECT
    YCL_CH09_TRDIRT VISIBLE LENGTH 30. " input program name

  SELECTION-SCREEN PUSHBUTTON 53(16) BUTTONCR
    USER-COMMAND CREAT. " push button 'Create'
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN SKIP 1. " a blank line

SELECTION-SCREEN BEGIN OF BLOCK BL1 WITH FRAME TITLE
 TEXT-003 NO INTERVALS. " block BL1 commencement

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS SRCE RADIOBUTTON GROUP GRP. " radio button Source
    SELECTION-SCREEN COMMENT 2(20) TEXT-004.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS VARA RADIOBUTTON GROUP GRP. " radio button Variants
    SELECTION-SCREEN COMMENT 2(20) TEXT-005.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS ATTR RADIOBUTTON GROUP GRP. " radio button Attributes
    SELECTION-SCREEN COMMENT 2(20) TEXT-006.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS DOCU RADIOBUTTON GROUP GRP. " radio button Documentation
    SELECTION-SCREEN COMMENT 2(20) TEXT-007.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS TXEL RADIOBUTTON GROUP GRP. " radio button Text elements
    SELECTION-SCREEN COMMENT 2(20) TEXT-008.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN  SKIP 1. " a blank line

  SELECTION-SCREEN BEGIN OF LINE.

    SELECTION-SCREEN PUSHBUTTON 01(16) BUTTONDS
     USER-COMMAND DISPL. " push button 'Display'
    SELECTION-SCREEN PUSHBUTTON 20(16) BUTTONCH
     USER-COMMAND CHANG. " push button 'Change'

  SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK BL1.

***********************************************************
* build button text + icon code through function module  **
***********************************************************
INITIALIZATION. " triggered once just after user presses F8

 PERFORM ASSEMBL_BUTTON_TEXT USING 'ICON_CREATE' TEXT-002 BUTTONCR.
                                        " TEXT-002 contains 'Create'

 PERFORM ASSEMBL_BUTTON_TEXT USING 'ICON_DISPLAY' TEXT-009 BUTTONDS.
                                        " TEXT-009 contains 'Display'

 PERFORM ASSEMBL_BUTTON_TEXT USING 'ICON_CHANGE' TEXT-010 BUTTONCH.
                                        " TEXT-010 contains 'Change'


***********************************************************
AT SELECTION-SCREEN.
CASE SY-UCOMM.
 WHEN 'CREAT'.
 MESSAGE I011 WITH 'Create'.
 WHEN 'DISPL'.
 MESSAGE I011 WITH 'Display'.
 WHEN 'CHANG'.
 MESSAGE I011 WITH 'Change'.
ENDCASE.
**********************************************************
FORM ASSEMBL_BUTTON_TEXT USING VALUE(ICON_NAME) TSYM RET_STR.
DATA: STR1 TYPE STRING,
      STR2 TYPE STRING,
      STR3 TYPE STRING.

CALL FUNCTION 'ICON_CREATE'
    EXPORTING
      name   = ICON_NAME
      text   = TSYM
      info   = ' '
    IMPORTING
      RESULT = RET_STR
    EXCEPTIONS
      OTHERS = 0.

**********************************************
* jugglery to remove the tool-tip/info text **
* from the button string                    **
**********************************************

SPLIT RET_STR AT '\' INTO STR1 STR2.
SPLIT STR2 AT '@' INTO STR3 STR2.
CONDENSE STR2.
CONCATENATE STR1 STR2 INTO RET_STR SEPARATED BY '@' .

ENDFORM.
